buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    }

}

plugins {
    id "com.jfrog.bintray" version "1.4"
    id "org.sonarqube" version "2.2"

}

apply plugin: 'com.android.library'
apply plugin: 'net.ltgt.errorprone'
apply plugin: 'com.jfrog.bintray'

allprojects {
    group = 'util.android.textviews'
    version = '2.0.3'
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def siteUrl = 'https://github.com/iNdieboyjeff/TextViews'   // Homepage URL of the library
def gitUrl = 'https://github.com/iNdieboyjeff/TextViews.git'   // Git repository URL
def issueUrl = 'https://github.com/iNdieboyjeff/TextViews/issues'




task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    title = 'TextViews ' + version
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/BuildConfig.java'
    options {
        links "http://docs.oracle.com/javase/7/docs/api/"
        linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
    }
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
afterEvaluate {
    javadoc.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "textviews"
        websiteUrl = siteUrl
        publicDownloadNumbers = true
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        labels = ["Android", "TextView", "Typeface", "Font", "Widget"]
        desc = "Custom TextView components for Android with Typeface (font) support."
        licenses = ["Apache-2.0"]
        publish = true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.0'
}

